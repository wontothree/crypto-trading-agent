// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © tradeforopp

//@version=5
indicator("Protected Highs & Lows [TFO]", "Protected Highs & Lows [TFO]", true, max_lines_count = 500, max_labels_count = 500, max_boxes_count = 500)

var g_STR = "Structure"
ps = input.int(1, "Pivot Strength", group = g_STR)
color_trend = input.bool(true, "Trend Candles", inline = "TREND", group = g_STR)
show_labels = input.bool(true, "Show Structure Labels", inline = "MSS", group = g_STR)
label_type = input.string("MSS", "", options = ['MSS', 'BOS', 'All'], inline = "MSS", group = g_STR)

var g_PHL = "Protected Highs & Lows"
show_phl = input.bool(true, "Show Protected Highs & Lows", inline = "PHL", group = g_PHL)
trail_phl = input.bool(true, "Show Protected Trail", inline = "TRAIL", group = g_PHL)
trail_width = input.int(2, "", inline = "TRAIL", group = g_PHL)

var g_PB = "Pullbacks"
use_valid_pbs = input.bool(true, "Use Valid Pullback Criteria", tooltip = "When looking at the swing high that price must close above for a BOS/MSS, in order to form a valid protected low, price must first close below the low of the candle that made the swing high (and vice versa for protected highs)", group = g_PB)
extra_valid_pbs = input.bool(false, "Specific Pullback Candle Criteria", tooltip = "When 'Use Valid Pullback Criteria' is enabled, if a given swing high is formed with a down close candle, the script will look backward to find the most recent up-close candle. Using this candle, price must close below its low to validate a pullback from a swing high (and vice versa)", group = g_PB)

ph_color = input.color(#f23645, "", inline = "PHL", group = g_PHL)
pl_color = input.color(color.blue, "", inline = "PHL", group = g_PHL)

bull_color = input.color(color.teal, "", inline = "TREND", group = g_STR)
bear_color = input.color(#f23645, "", inline = "TREND", group = g_STR)

var g_RET = "Retracements"
show_rets = input.bool(false, "Show Retracements", tooltip = "Show retracements of the previous zone with 30, 50, and 70% retracement levels", group = g_RET)
box_transp = input.int(85, "Box Transparency", 0, 100, tooltip = "Larger values will make the retracement box more transparent", group = g_RET)
line_w = input.int(1, "Line Width", 0, 10, group = g_RET)
line_s = input.string('Dotted', "Line Style", options = ['Solid', 'Dashed', 'Dotted'], group = g_RET)


var style = switch line_s // 선의 스타일
    'Solid' => line.style_solid
    'Dashed' => line.style_dashed
    'Dotted' => line.style_dotted

var bool bull = na // 현재의 트렌드가 양(상승)인지 음(하락)인지를 나타낸다.

var float trail_price = na // 트레일의 가격
var color trail_color = na // 트레일의 색상

var ph = array.new_float() // protected high를 저장하는 배열
var pl = array.new_float() // protected low

var pht = array.new_int() // protected high의 시간 정보를 저장하는 배열
var plt = array.new_int() // protected low의 시간 정보를 저장하는 배열

var float last_high = na // 이전 최고점
var float last_low = na // 이전 최저점

var int last_high_idx = na // 이전 최고점이 발생한 인덱스
var int last_low_idx = na // 이전 최저점이 발생한 인덱스

var float track_high = na // 현재 추적 중인 최고점
var float track_low = na // 현재 추적 중인 최저점

var int track_high_idx = na // 현재 추적 중인 최고점의 인덱스
var int track_low_idx = na // 현재 추적 중인 최저점의 인덱스

// 사용자 정의 타입
type pb // Pullback과 관련된 정보
    float price // 풀백의 가격
    int idx // 풀백이 발생한 인덱스
    bool valid = false // 풀백이 유효한지 여부
    bool bull // 풀백이 상승(양) 트렌드에 관련된 것인지 하락(음) 트렌드에 관련된 것인지 여부

type snd // Sound에 관련된 정보
    box _box // 차트에 표시되는 상자를 나타내는 객체
    line _30 // 차트에 표시되는 30% 수준의 선
    line _50 // 차트에 표시되는 50% 수준의 선
    line _70 // 차트에 표시되는 70% 수준의 선

type dwg // Drawing에 관련된 정보
    label[] _label // 차트에 표시되는 label을 나타내는 배열
    label[] _phl // 차트에 표시되는 다른 종류의 레이블을 나타내는 배열
    line[] _line // 차트에 표시되는 선을 나타내는 배열
    bool[] _bull // 차트에 표시되는 다른 정보를 나타내는 부울 배열

// snd 데이터 타입에 대한 새로운 정보를 추가하는 함수
method snd_add(snd s, float _high, float _low, bool bull) => // 인스턴스, 최고점의 가격, 최저점의 가격, 상승(양) 트렌드 여부
    _30 = (_high - _low) * 0.3 + _low
    _50 = (_high - _low) * 0.5 + _low
    _70 = (_high - _low) * 0.7 + _low
    s._box := box.new(time, _high, time, _low, xloc = xloc.bar_time, border_color = na, bgcolor = na)
    s._30 := line.new(time, _30, time, _30, xloc = xloc.bar_time, color = na, style = style, width = line_w)
    s._50 := line.new(time, _50, time, _50, xloc = xloc.bar_time, color = na, style = style, width = line_w)
    s._70 := line.new(time, _70, time, _70, xloc = xloc.bar_time, color = na, style = style, width = line_w)

// dwg 데이터 타입에 대한 새로운 정보를 추가하는 함수
method dwg_add(dwg d, label LB, label PHL, line LN, bool BL) => // 인스턴스, 차트에 표시될 레이블, 차트에 표시될 다른 종류의 레이블, 차트에 표시될 선, 차트에 표시될 다른 정보
    d._label.unshift(LB)
    d._phl.unshift(PHL)
    d._line.unshift(LN)
    d._bull.unshift(BL)

// pb 데이터 타입에 대한 새로운 정보를 추가하는 함수
method pb_set(pb p, float P, int I) => // 인스턴스, 풀백의 가격, 풀백이 발생한 인덱스
    p.price := P
    p.idx := I
    p.valid := false

clear_all() => // pl, plt, ph, pht 배열 초기화
    pl.clear()
    plt.clear()    
    ph.clear()
    pht.clear()

var pb_from_high = pb.new(bull = true) // 상승(양) 트렌드를 나타내는 pb 타입 객체
var pb_from_low  = pb.new(bull = false) // 하락(음) 트렌드를 나타내는 pb 타입 객체

if ta.pivotlow(low, ps, ps) and pl.size() == 0 // 최근 저가(low)의 피봇을 찾는다.
    pl.unshift(low[ps])
    plt.unshift(time[ps])

    if extra_valid_pbs // 추가적인 검사
        for i = 0 to 3
            if close[ps + i] < open[ps + i]
                pb_set(pb_from_low, high[ps + i], bar_index) // 객체 업데이트
                break
    else
        pb_set(pb_from_low, high[ps], bar_index)

    if na(last_low)
        last_low := low[ps]
        last_low_idx := bar_index - ps
    else
        if low[ps] < last_low
            last_low := low[ps]
            last_low_idx := bar_index - ps
if ta.pivothigh(high, ps, ps) and ph.size() == 0 // 최근 고가(high)의 피봇을 찾는다.
    ph.unshift(high[ps])
    pht.unshift(time[ps])
    
    if extra_valid_pbs // 추가적인 검사
        for i = 0 to 3
            if close[ps + i] > open[ps + i]
                pb_set(pb_from_high, low[ps + i], bar_index)
                break
    else
        pb_set(pb_from_high, low[ps], bar_index)
    
    if na(last_high)
        last_high := high[ps]
        last_high_idx := bar_index - ps
    else
        if high[ps] > last_high
            last_high := high[ps]
            last_high_idx := bar_index - ps

check_pb(pb p) =>
    if p.bull // 피봇이 상승(양) 트렌드 
        if close < p.price and p.valid == false // 현재 가격이 피봇 가격보다 낮고, 피봇이 아직 유효하지 않다.
            p.valid := true
    else
        if close > p.price and p.valid == false
            p.valid := true

check_pb(pb_from_high) // 피봇 유효성 검사
check_pb(pb_from_low)

if (high[ps] > track_high or na(track_high) or last_low_idx >= track_high_idx) and not na(ta.pivothigh(high, ps, ps)) and (use_valid_pbs ? pb_from_low.valid == true : true) // 현재 고가(high[ps])가 추적 중인 고가(track_high)보다 크거나, 추적 중인 고가가 정의되지 않았거나, 기록한 낮은 인덱스(last_low_idx)가 현재 추적 중인 고가의 인덱스(track_high_idx)보다 크거나 같으면서 신규 피봇이 확인됐다.
    track_high := high[ps] // 업데이트
    track_high_idx := bar_index - ps // 업데이트
if (low[ps] < track_low or na(track_low) or last_high_idx >= track_low_idx) and not na(ta.pivotlow(low, ps, ps)) and (use_valid_pbs ? pb_from_high.valid == true : true)
    track_low := low[ps]
    track_low_idx := bar_index - ps

bos_bear = false
bos_bull = false
mss_bear = false
mss_bull = false
change = false

var dwgs = dwg.new(array.new_label(), array.new_label(), array.new_line(), array.new_bool())
var snd = snd.new()

// snd 객체를 유지보수하는 메서드 : 객체가 null이 아닌 경우에만 해당 객체의 속성들을 업데이트한다.
maintain(snd s) =>
    if not na(s._box)
        s._box.set_right(time)
        s._30.set_x2(time)
        s._50.set_x2(time)
        s._70.set_x2(time)

maintain(snd)

if ph.size() > 0 // 현재까지 기록된 상승 피봇이 하나 이상 있는 경우
    if close > ph.get(0) // 현재 종가가 가장 최근의 상승 피봇의 고가를 넘는다.
        label _label = na
        label _phl = na

        if show_labels // 라벨을 표시하는 옵션이 활성화됐다.
            save = false
            if label_type == 'MSS' and not bull
                save := true
            else if label_type == 'BOS' and bull
                save := true
            else if label_type == 'All'
                save := true
            if save    
                _label := label.new(math.floor(math.avg(time, pht.get(0))), ph.get(0), bull ? "BOS" : "MSS", xloc = xloc.bar_time, style = label.style_label_down, color = #ffffff00, textcolor = na)

        if bull
            bos_bull := true
        else
            mss_bull := true

        if show_rets and (use_valid_pbs ? pb_from_high.valid == true : true)
            snd.snd_add(ph.get(0), track_low, true)

        _line = line.new(pht.get(0), ph.get(0), time, ph.get(0), color = na, xloc = xloc.bar_time, style = line.style_dashed)
        bull := true
        change := true

        clear_all()

        if not na(track_low)
            if show_phl
                _phl := label.new(time[bar_index - track_low_idx], track_low, "▲", xloc = xloc.bar_time, style = label.style_label_up, textcolor = na, color = #ffffff00)

            pl.unshift(track_low)
            plt.unshift(time[bar_index - track_low_idx])
            last_high := na

        dwgs.dwg_add(_label, _phl, _line, bull)

if pl.size() > 0
    if close < pl.get(0) // 현재 종가가 이전에 감지된 보호된 저점보다 낮다.
        label _label = na
        label _phl = na

        if show_labels
            save = false
            if label_type == 'MSS' and bull
                save := true
            else if label_type == 'BOS' and not bull
                save := true
            else if label_type == 'All'
                save := true
            if save    
                _label := label.new(math.floor(math.avg(time, plt.get(0))), pl.get(0), not bull ? "BOS" : "MSS", xloc = xloc.bar_time, style = label.style_label_up, color = #ffffff00, textcolor = na)

        if not bull
            bos_bear := true
        else
            mss_bear := true

        if show_rets and (use_valid_pbs ? pb_from_low.valid == true : true)
            snd.snd_add(pl.get(0), track_high, false)

        _line = line.new(plt.get(0), pl.get(0), time, pl.get(0), color = na, xloc = xloc.bar_time, style = line.style_dashed)
        bull := false
        change := true
                
        clear_all()

        if not na(track_high)
            if show_phl
                _phl := label.new(time[bar_index - track_high_idx], track_high, "▼", xloc = xloc.bar_time, style = label.style_label_down, textcolor = na, color = #ffffff00)

            ph.unshift(track_high)
            pht.unshift(time[bar_index - track_high_idx])                
            last_low := na
            
        dwgs.dwg_add(_label, _phl, _line, bull)

// 조건 확인 및 값 할당
if change[1]
    if bos_bear[1] or mss_bear[1]
        trail_price := track_high
        trail_color := ph_color
    else if bos_bull[1] or mss_bull[1]
        trail_price := track_low
        trail_color := pl_color
    
    // 도형 및 색상 설정
    _bull = dwgs._bull.get(0)
    dwgs._label.get(0).set_textcolor(_bull ? bull_color : bear_color)
    dwgs._phl.get(0).set_textcolor(_bull ? pl_color : ph_color)
    dwgs._line.get(0).set_color(_bull ? bull_color : bear_color)

    snd._box.set_bgcolor(color.new(bull ? bull_color : bear_color, box_transp))
    snd._30.set_color(_bull ? bull_color : bear_color)
    snd._50.set_color(_bull ? bull_color : bear_color)
    snd._70.set_color(_bull ? bull_color : bear_color)

// 차트 및 플롯 설정
barcolor(color_trend ? (bull ? bull_color : bear_color) : na)

plot(trail_phl ? trail_price : na, color = trail_color, linewidth = trail_width)

// 알림 설정
alertcondition(bos_bear[1] or bos_bull[1], "BOS Any")
alertcondition(mss_bear[1] or mss_bull[1], "MSS Any")

alertcondition(bos_bear[1], "BOS Bear")
alertcondition(bos_bull[1], "BOS Bull")

alertcondition(mss_bear[1], "MSS Bear")
alertcondition(mss_bull[1], "MSS Bull")